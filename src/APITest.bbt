REM >PrintPDFAPITest
REM
REM Copyright 2023, Stephen Fryatt (info@stevefryatt.org.uk)
REM
REM This file is part of PrintPDFAPITest:
REM
REM   http://www.stevefryatt.org.uk/risc-os
REM
REM Licensed under the EUPL, Version 1.2 only (the "Licence");
REM You may not use this work except in compliance with the
REM Licence.
REM
REM You may obtain a copy of the Licence at:
REM
REM   http://joinup.ec.europa.eu/software/page/eupl
REM
REM Unless required by applicable law or agreed to in
REM writing, software distributed under the Licence is
REM distributed on an "AS IS" basis, WITHOUT WARRANTIES
REM OR CONDITIONS OF ANY KIND, either express or implied.
REM
REM See the Licence for the specific language governing
REM permissions and limitations under the Licence.

LIBRARY "BASIC:Icon"
LIBRARY "BASIC:Menu"
LIBRARY "BASIC:Message"
LIBRARY "BASIC:Resources"
LIBRARY "BASIC:String"
LIBRARY "BASIC:Template"
LIBRARY "BASIC:Url"
LIBRARY "BASIC:WimpError"
LIBRARY "BASIC:Window"

LIBRARY "src/DataXfer.bbt"
LIBRARY "src/IHelp.bbt"
LIBRARY "src/InfoWind.bbt"
LIBRARY "src/MainWind.bbt"
LIBRARY "src/Protocol.bbt"

REM Set up the basic error handling, then initialise.

PROCwimperror_initialise("PrintPDFAPITest", "!PrintPDFAPITest")
ON ERROR result% = FNwimperror_program : END

PrintPDFAPITestTaskHandle% = -1

PROCapi_test_initialise

REM Poll the Wimp until exit.

Quit%=FALSE

ON ERROR Quit% = FNwimperror_program

WHILE NOT Quit%
	PROCapi_test_poll
ENDWHILE

IF PrintPDFAPITestTaskHandle% <> -1 THEN SYS "Wimp_CloseDown", PrintPDFAPITestTaskHandle%, &4B534154
END


REM Handle calls to Wimp_Poll and process the responses.
:
DEF PROCapi_test_poll
LOCAL reason%

SYS "Wimp_Poll", %110001, b% TO reason%

CASE reason% OF
	WHEN 2:		SYS "Wimp_OpenWindow",,b%
	WHEN 3:		SYS "Wimp_CloseWindow",,b%
	WHEN 6:		PROCapi_test_mouse_click(b%)
	WHEN 7:		PROCmain_window_drag_box
	WHEN 8:		PROCapi_test_keypress(b%)
	WHEN 9 :	PROCapi_test_menu_selection(b%)
	WHEN 17,18 :	PROCapi_test_process_message(b%)
	WHEN 19 :	PROCapi_test_process_bounce(b%)
ENDCASE
ENDPROC


REM Handle mouse clicks in PrintPDFAPITest windows.
REM
REM \param pointer%	Pointer to the Wimp_Poll block.
:
DEF PROCapi_test_mouse_click(pointer%)
CASE pointer%!12 OF
WHEN -2
	CASE pointer%!8 OF
	WHEN 2
		PROCmenu_create_iconbar(IconbarMenu%, pointer%!0)
	WHEN 4
		PROCmain_window_toggle(pointer%!0, pointer%!4)
	ENDCASE

WHEN MainWindHandle%
	IF pointer%!8 = 2 THEN
		PROCmenu_create(IconbarMenu%, pointer%!0, pointer%!4)
	ELSE
		PROCmain_wind_click(pointer%!8, pointer%!16)
	ENDIF

WHEN InfoWindHandle%
	PROCinfo_wind_click(pointer%!8, pointer%!16)
ENDCASE
ENDPROC


REM Handle keypresses in PrintPDFAPITest windows.
REM
REM \param keypress%	Pointer to the Wimp_Poll block
:
DEF PROCapi_test_keypress(keypress%)
CASE keypress%!0 OF
WHEN MainWindHandle%
	PROCmain_wind_keypress(keypress%!24)
OTHERWISE
	SYS "Wimp_ProcessKey", keypress%!24
ENDCASE
ENDPROC


REM Handle selections from the iconbar menu.
REM
REM \param selection%	Pointer to the menu selection block.
:
DEF PROCapi_test_menu_selection(selection%)
IF FNmenu_current_handle <> IconbarMenu% THEN ENDPROC

CASE selection%!0 OF
	WHEN 1		: OSCLI("Filer_Run <PrintPDFAPITest$Dir>.!Help")
	WHEN 2		: Quit% = TRUE
ENDCASE
ENDPROC


REM Handle incoming Wimp messages.
REM
REM \param message%	Pointer to the Wimp_Poll block.
:
DEF PROCapi_test_process_message(message%)
CASE b%!16 OF
	WHEN 0		: Quit% = TRUE : REM Message_Quit
	WHEN 2		: PROCdataxfer_message_data_save_ack(message%)
	WHEN &502	: PROCihelp_send(message%)
	WHEN &400C2	: PROCprotocol_message_task_initialise(message%)
	WHEN &400C3	: PROCprotocol_message_task_close_down(message%)
ENDCASE
ENDPROC


REM Handle bounced Wimp messages.
REM
REM \param message%	Pointer to the Wimp_Poll block.
:
DEF PROCapi_test_process_bounce(message%)
CASE b%!16 OF
	WHEN 1			: PROCdataxfer_message_bounce(message%)
	WHEN &4E383, &4AF80	: PROCurl_bounce(message%) : REM Message_URIReturnResult, Message_InetSuiteOpenURL
	OTHERWISE		: PROCprotocol_message_bounce(message%)
ENDCASE
ENDPROC


REM Initialise PrintPDFAPITest, both internally and with the Wimp.
:
DEF PROCapi_test_initialise
LOCAL resources$

DIM b% 1024, q% 256, a% 255

REM Locate the application resources.

PROCresources_initialise_paths("<PrintPDFAPITest$Dir>.Resources", "UK", "PrintPDFAPITest$Language")

REM Load the messages file.

resources$ = FNresources_find_file("Messages", &FFF)
IF resources$ = "" THEN ERROR 255, "Unable to find Messages file"

PROCmessage_initialise(resources$)

TaskName$ = FNmessage_lookup("TaskName")
TaskSprite$ = FNmessage_lookup("TaskSpr")
PROCwimperror_initialise(TaskName$, TaskSprite$)

REM Initialise with the Wimp.

q%!0  = &2
q%!4  = &502
q%!8  = &400C2
q%!12 = &400C3
q%!16 = &4E383
q%!20 = &4AF80
q%!24 = 0

SYS "Wimp_Initialise", 310, &4B534154, TaskName$, q% TO ,PrintPDFAPITestTaskHandle%

resources$ = FNresources_find_file("Templates", &FEC)
IF resources$ = "" THEN ERROR 255, "Unable to find Templates file"

PROCtemplate_open(resources$)
PROCinfo_wind_initialise
PROCmain_wind_initialise
PROCtemplate_close

REM Create the iconbar icon

b%!0 = -1
b%!4 = 0
b%!8 = 0
b%!12 = 76
b%!16 = 68
b%!20 = %11000000011010
$(b%+24) = TaskSprite$
SYS "Wimp_CreateIcon",,b%

REM Initialise and load the menu templates

resources$ = FNresources_find_file("Menus", &FFD)
IF resources$ = "" THEN ERROR 255, "Unable to find Menus file"

PROCmenu_initialise

b%!0 = InfoWindHandle%
PROCmenu_load_templates(resources$, b%)
IconbarMenu% = b%!0

REM Set the task handle in the main window.

PROCmain_wind_set_our_task_handle(PrintPDFAPITestTaskHandle%)

REM Initialise the data transfer protocol support

PROCdataxfer_initialise

REM Initialise the protocol support

PROCprotocol_initialise
ENDPROC
