REM >MainWind
REM
REM Copyright 2023, Stephen Fryatt (info@stevefryatt.org.uk)
REM
REM This file is part of PrintPDFAPITest:
REM
REM   http://www.stevefryatt.org.uk/risc-os
REM
REM Licensed under the EUPL, Version 1.2 only (the "Licence");
REM You may not use this work except in compliance with the
REM Licence.
REM
REM You may obtain a copy of the Licence at:
REM
REM   http://joinup.ec.europa.eu/software/page/eupl
REM
REM Unless required by applicable law or agreed to in
REM writing, software distributed under the Licence is
REM distributed on an "AS IS" basis, WITHOUT WARRANTIES
REM OR CONDITIONS OF ANY KIND, either express or implied.
REM
REM See the Licence for the specific language governing
REM permissions and limitations under the Licence.


REM Load the main window template and initialise the variables.
:
DEF PROCmain_wind_initialise
PROCtemplate_load_claim("Main", b%, 1024, -1)
SYS "Wimp_CreateWindow",,b% TO MainWindHandle%

REM The icon handles.

MainWindOurTaskHandleIcon% = 0
MainWindPrintPDFTaskHandleIcon% = 2
MainWindFileNameIcon% = 6
MainWindFileSprIcon% = 8

REM Initialise the filename.

PROCmain_wind_set_filename("")
ENDPROC


REM Handle clicks in the main window.
REM
REM \param button%	The mouse button used for the click.
REM \param icon%	The icon over which the click occurred.
:
DEF PROCmain_wind_click(button%, icon%)
IF button% <> 4 THEN ENDPROC

CASE icon% OF
WHEN MainWindFileSprIcon%
	PROCmain_wind_drag_icon(MainWindHandle%, MainWindFileSprIcon%)
ENDCASE
ENDPROC


REM Handle keypresses in the main window.
REM
REM \param key%		The key which was pressed.
:
DEF PROCmain_wind_keypress(key%)
CASE key% OF
WHEN 13 :
WHEN 27 : PROCmain_wind_close
OTHERWISE : SYS "Wimp_ProcessKey", key%
ENDCASE
ENDPROC


REM Toggle the state of the main window, opening it if it
REM is closed, or closing it if it is open.
REM
REM \param x%		The X coordinate for the centre of the window.
REM \param y%		The Y coordinate for the centre of the window.
:
DEF PROCmain_window_toggle(x%, y%)
IF FNwindow_is_open(MainWindHandle%) THEN
	PROCwindow_close(MainWindHandle%)
ELSE
	PROCwindow_open_centred_at(MainWindHandle%, x%, y%)
	PROCicon_put_caret_at_end(MainWindHandle%, MainWindFileNameIcon%)
ENDIF
ENDPROC


REM Close the main window.
:
DEF PROCmain_wind_close
PROCwindow_close(MainWindHandle%)
ENDPROC


REM Set our task handle field to a task handle.
REM
REM \param taskhandle$	The handle of the task owning the content.
:
DEF PROCmain_wind_set_our_task_handle(taskhandle%)
LOCAL display$

IF taskhandle% <> -1 THEN
	display$ = "0x" + STR$~(taskhandle%)
ELSE
	display$ = FNmessage_lookup("NoTask")
ENDIF

PROCicon_set_indirected_text(MainWindHandle%, MainWindOurTaskHandleIcon%, display$)
PROCicon_redraw(MainWindHandle%, MainWindOurTaskHandleIcon%)
ENDPROC


REM Set the PrintPDF task handle field to a task handle.
REM
REM \param taskhandle$	The handle of the task owning the content.
:
DEF PROCmain_wind_set_printpdf_task_handle(taskhandle%)
LOCAL display$

IF taskhandle% <> -1 THEN
	display$ = "0x" + STR$~(taskhandle%)
ELSE
	display$ = FNmessage_lookup("NoTask")
ENDIF

PROCicon_set_indirected_text(MainWindHandle%, MainWindPrintPDFTaskHandleIcon%, display$)
PROCicon_redraw(MainWindHandle%, MainWindPrintPDFTaskHandleIcon%)
ENDPROC


REM Set the filename field content.
REM
REM \param filename$	The requiredfilename.
:
DEF PROCmain_wind_set_filename(filename$)
LOCAL name$

IF filename$ = "" THEN name$ = FNmessage_lookup("DefaultFile") ELSE name$ = filename$

PROCicon_set_indirected_text(MainWindHandle%, MainWindFileNameIcon%, name$)

SYS "Wimp_GetCaretPosition",,q%
IF q%!0 = MainWindHandle% THEN
	IF filename$ = "" THEN
		PROCicon_put_caret(q%!0, -1, 0)
	ELSE
		PROCicon_put_caret_at_end(q%!0, q%!4)
	ENDIF
ENDIF

PROCicon_redraw(MainWindHandle%, MainWindFileNameIcon%)
ENDPROC


REM Get the current filename from the filename field.
REM
REM \return		The filename in the field.
:
DEF FNmain_wind_get_filename
=FNicon_get_text(MainWindHandle%, MainWindFileNameIcon%)


REM Process drags on a file save icon.
REM
REM \param window%	The handle of the window.
REM \param icon%	The handle of the icon.
:
DEF PROCmain_wind_drag_icon(!q%, q%!4)
LOCAL minx%, miny%, maxx%, maxy%, xoff%

SYS "Wimp_GetIconState",,q%

minx% = q%!8
miny% = q%!12
maxx% = q%!16
maxy% = q%!20

SYS "Wimp_GetWindowState",,q%

xoff% = q%!4

q%!0 = minx% + xoff%
q%!4 = miny% + q%!16
q%!8 = maxx% + xoff%
q%!12 = maxy% + q%!16

SYS "DragASprite_Start", %11000101, 1, "file_adf", q%
ENDPROC


REM Handle User Drag Box events, signalling the end of a drag
REM for our file icon.
:
DEF PROCmain_window_drag_box
SYS "DragASprite_Stop"
SYS "Wimp_GetPointerInfo",,b%
IF b%!12 = MainWindHandle% THEN ENDPROC
PROCdataxfer_start_drag_save(b%!0, b%!4, FNstring_leafname(FNmain_wind_get_filename), b%!12, b%!16)
ENDPROC
